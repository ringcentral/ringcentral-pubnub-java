plugins {
  id 'java'
  id 'maven-publish'
  id 'java-library'
  id 'signing'
}

group 'com.ringcentral'
version '1.3.0'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  implementation group: 'com.pubnub', name: 'pubnub-gson', version: '5.2.3'
  implementation group: 'com.ringcentral', name: 'ringcentral', version: '2.9.0'
  testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = 'ringcentral-pubnub'
      from components.java
      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }
      pom {
        name = 'RingCentral PubNub SDK'
        description = 'RingCentral PubNub SDK'
        url = 'https://github.com/ringcentral/ringcentral-pubnub-java'
        licenses {
          license {
            name = 'MIT'
            url = 'https://opensource.org/licenses/MIT'
          }
        }
        developers {
          developer {
            id = 'tylerliu'
            name = 'Tyler Liu'
            email = 'tyler.liu@ringcentral.com'
          }
        }
        scm {
          connection = 'scm:git:git://git@github.com:ringcentral/ringcentral-pubnub-java.git'
          developerConnection = 'scm:git:ssh://git@github.com:ringcentral/ringcentral-pubnub-java.git'
          url = 'https://github.com/ringcentral/ringcentral-pubnub-java'
        }
      }
    }
  }
  repositories {
    maven {
      url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = ossrhUsername
        password = ossrhPassword
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

javadoc {
  options.addBooleanOption('html5', true)
}

test {
  testLogging {
    exceptionFormat = 'full'
  }
}