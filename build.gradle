plugins {
    id 'java'
    id 'maven'
    id 'java-library'
    id 'signing'
}

group 'com.ringcentral'
version '1.0.0'
archivesBaseName = "ringcentral-pubnub"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.pubnub', name: 'pubnub-gson', version: '4.29.1'
    implementation group: 'com.ringcentral', name: 'ringcentral', version: '2.4.0'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}

javadoc {
    options.addBooleanOption('html5', true)
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}
signing {
    sign configurations.archives
}

java {
    withJavadocJar()
    withSourcesJar()
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
        if (project.hasProperty('ossrhUsername')) {
            authentication(userName: ossrhUsername, password: ossrhPassword)
        }
      }

      snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
        if (project.hasProperty('ossrhUsername')) {
            authentication(userName: ossrhUsername, password: ossrhPassword)
        }
      }

      pom.project {
        name 'RingCentral PubNub SDK'
        packaging 'jar'
        artifactId 'ringcentral-pubnub'
        description 'RingCentral PubNub SDK for Java'
        url 'https://github.com/ringcentral/ringcentral-pubnub-java'
        licenses {
            license {
                name "MIT"
                url "https://opensource.org/licenses/MIT"
            }
        }
        developers {
            developer {
                id 'tylerliu'
                name 'Tyler Liu'
                email 'tyler.liu@ringcentral.com'
            }
        }
        scm {
            connection 'scm:git:git://git@github.com:ringcentral/ringcentral-pubnub-java.git'
            developerConnection 'scm:git:ssh://git@github.com:ringcentral/ringcentral-pubnub-java.git'
            url 'https://github.com/ringcentral/ringcentral-pubnub-java'
        }
      }
    }
  }
}
