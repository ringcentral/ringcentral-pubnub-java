plugins {
    id 'java'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven'
    id 'maven-publish'
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
}

group 'com.ringcentral'
version '1.0.0'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile group: 'com.pubnub', name: 'pubnub-gson', version: '4.29.1'
    compile group: 'com.ringcentral', name: 'ringcentral', version: '1.0.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testRuntime group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

def pomConfig = {
    licenses {
        license {
            name "MIT"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "tylerlong"
            name "Tyler Liu"
            email "tyler.liu@ringcentral.com"
        }
    }
    scm {
        url "https://github.com/ringcentral/ringcentral-pubnub-java"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.ringcentral'
            artifactId 'ringcentral-pubnub'
            version '1.0.0'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'RingCentral PubNub SDK')
                root.appendNode('name', 'ringcentral-pubnub')
                root.appendNode('url', 'https://github.com/ringcentral/ringcentral-pubnub-java')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    publish = true
    override = true
    pkg {
        repo = 'maven'
        name = 'ringcentral-pubnub'
        userOrg = user
        desc = 'RingCentral PubNub SDK'
        licenses = ['MIT']
        websiteUrl = 'https://github.com/ringcentral/ringcentral-pubnub-java'
        issueTrackerUrl = 'https://github.com/ringcentral/ringcentral-pubnub-java/issues'
        vcsUrl = 'https://github.com/ringcentral/ringcentral-pubnub-java.git'
        labels = ['RingCentral', 'PubNub']
        publicDownloadNumbers = true
        githubRepo = 'ringcentral/ringcentral-pubnub-java' //Optional Github repository
        githubReleaseNotesFile = 'README.md' //Optional Github readme file
        version {
            name = '1.0.0'
            desc = 'RingCentral PubNub SDK 1.0.0'
            vcsTag = '1.0.0'
            gpg {
                sign = true
            }
        }
    }
}
